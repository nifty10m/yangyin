plugins {
    // Apply the groovy plugin to add support for Groovy
    id 'groovy'
    id 'maven-publish'
    id 'signing'
    id 'com.palantir.git-version' version '0.12.0-rc1'
}

repositories {
    // Use jcenter for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    mavenCentral()
}

group = 'io.github.nifty10m.yangying'
version = gitVersion()

dependencies {
    // Use the latest Groovy version for building this library
    implementation 'org.codehaus.groovy:groovy-all:2.5.4'
    implementation 'org.spockframework:spock-core:1.2-groovy-2.5'

    // Use the awesome Spock testing and specification framework
    testImplementation 'org.spockframework:spock-core:1.2-groovy-2.5'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'io.github.nifty10m'
            artifactId = 'yangying'
            version = gitVersion()
            from components.java
        }
    }
    repositories {
        maven {
            def snapshotRepoUrl =  "https://oss.sonatype.org/content/repositories/snapshots"
            def releaseRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            url = version.matches("\\d\\.\\d\\.\\d") ? releaseRepoUrl : snapshotRepoUrl
            credentials {
                username System.getenv("SONATYPE_USER")
                password System.getenv("SONATYPE_PASSWORD")
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}